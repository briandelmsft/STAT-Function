let mde_ids = dynamic({mde_id_list});
let selected_nodes =
ExposureGraphNodes
| where NodeLabel in~ ('user','device','entra-userCookie','microsoft.compute/virtualmachines');
let selected_edges = 
ExposureGraphEdges
| where EdgeLabel in~ ('has credentials of','contains', 'can authenticate as');
selected_edges
| make-graph SourceNodeId --> TargetNodeId with selected_nodes on NodeId
| graph-match (device)-[hasCreds*1..2]->(user)
 where device.EntityIds has_any (mde_ids)
 project 
 Computer=device.NodeName,
 ComputerCrit = coalesce(device.NodeProperties.rawData.criticalityLevel.criticalityLevel, dynamic(3)),
 ComputerCriticalityRules = device.NodeProperties.rawData.criticalityLevel.ruleNames,
 ComputerMaxCVSSScore=device.NodeProperties.rawData.highRiskVulnerabilityInsights.maxCvssScore,
 ComputerVulnerabilities=device.NodeProperties.rawData.highRiskVulnerabilityInsights,
 ComputerOnboarding=device.NodeProperties.rawData.onboardingStatus,
 ComputerSensorHealth=device.NodeProperties.rawData.sensorHealthState,
 ComputerRiskScore=device.NodeProperties.rawData.riskScore,
 ComputerExposureScore=device.NodeProperties.rawData.exposureScore,
 ComputerManualTags=device.NodeProperties.rawData.deviceManualTags,
 ComputerDynamicTags=device.NodeProperties.rawData.deviceDynamicTags,
 EdgeLabel=hasCreds.EdgeLabel,
 UsersOnDevice=coalesce(user.NodeProperties.rawData.accountUpn,user.NodeProperties.rawData.accountName,user.NodeProperties.rawData.distinguishedName,user.NodeProperties.rawData.accountDisplayName,user.NodeProperties.rawData.accountObjectId),
 UserCrit=coalesce(user.NodeProperties.rawData.criticalityLevel.criticalityLevel, dynamic(3)),
 UserCriticalityRules=user.NodeProperties.rawData.criticalityLevel.ruleNames,
 device, user
| extend ComputerTags = array_concat(ComputerManualTags, ComputerDynamicTags)
| where array_length(set_difference(EdgeLabel, dynamic(['contains']))) > 0
| sort by toint(ComputerCrit) asc, toint(Computer) asc
| project Computer, ComputerCrit, ComputerCriticalityRules, ComputerRiskScore, ComputerExposureScore, ComputerMaxCVSSScore, ComputerOnboarding, ComputerSensorHealth, ComputerTags
    , UsersOnDevice, UserCrit, UserCriticalityRules //, device, user, EdgeLabel